#!/usr/bin/env zsh

set -o errexit
set -o noglob
set -o nounset
set -o pipefail

# Enable this to assist with debugging errors.
# set -o xtrace

# Pretty print the environment variables in alphabetical order.
#
# Arguments:
#   A filter to fuzzy find on (optional).
#
# Usage:
#   penv [FILTER]
#
# Examples:
#   penv
#   penv XDG
#   penv TERM
function main() {
  # The `f` parameter expansion flag splits the result of the expansion at
  # newlines. This is a shorthand for `ps:\n:`.
  local -r env_vars=("${(f)$(env)}")
  local -r filter="${1:-}"
  local filtered=()

  for line in ${env_vars}; do
    if [[ -n "${filter}" ]] && [[ "${line}" != *"${filter}"* ]]; then
      continue
    fi

    filtered+=("${line}")
  done


  # Pipe to `cat -v` to display non-printing characters so they are visible.
  #
  # We need to reference this variable as an array so we can't quote it.
  # shellcheck disable=SC2086
  print -lo ${filtered} | cat -v
}

main "${@}"
